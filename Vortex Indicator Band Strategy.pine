Script Name: Vortex Indicator Band Strategy
Author: Traaaderz
Description: This strategy utilizing the Vortex indicator and its bands is effective when paired with displaced moving averages and a stop loss. Settings can be customized to match the specific ticker.
PineScript code:

Pine Script™ strategy
Vortex Indicator Band Strategy
Copy code
1
2
3
4
5
6
7
8
9
10
11
12
13
14
15
16
17
18
19
20
21
22
23
24
25
26
27
28
29
30
31
32
33
34
35
36
37
38
39
40
41
42
43
44
45
46
47
48
49
50
51
52
53
54
55
56
57
58
59
60
61
62
63
64
65
66
67
68
69
70
71
72
73
74
75
76
77
78
//@version=4
strategy(title = "Vortex Indicator Band Strategy", shorttitle="VIB Strategy", format=format.price, precision=4)
period_ = input(15, title="Period", minval=2)
vortexSmoothPeriod = input(10, title="Vortex Smoothing Period", minval=2)
bandRangePeriod = input(35, title="Band Range Period", minval=2)
bandSmoothingPeriod = input(19, title="Band Smoothing Period", minval=2)
stopLoss = input(0.15, title="Stop Loss", minval=.01)
//T3
b = 0.7
c1 = -b*b*b
c2 = 3*b*b+3*b*b*b
c3 = -6*b*b-3*b-3*b*b*b
c4 = 1+3*b+b*b*b+3*b*b
t3(src, len) => c1 * ema(ema(ema(ema(ema(ema(src, len), len), len), len), len), len) + c2 * ema(ema(ema(ema(ema(src, len), len), len), len), len) + c3 * ema(ema(ema(ema(src, len), len), len), len) + c4 * ema(ema(ema(src, len), len), len)
//T3 end
VMP = sum( abs( high - low[1]), period_ )
VMM = sum( abs( low - high[1]), period_ )
STR = sum( atr(1), period_ )
VIP = VMP / STR
VIM = VMM / STR
vipSmooth = t3(VIP, vortexSmoothPeriod)
vimSmooth = t3(VIM, vortexSmoothPeriod)
plot(vipSmooth, title="VI + Ma", color=#3BB3E4)
plot(vimSmooth, title="VI - Ma", color=#FF006E)
rangeVHigh = t3((highest(abs(vipSmooth), bandRangePeriod)+highest(abs(vimSmooth), bandRangePeriod))/2, bandSmoothingPeriod)
rangeVLow = 2-t3((highest(abs(vipSmooth), bandRangePeriod)+highest(abs(vimSmooth), bandRangePeriod))/2, bandSmoothingPeriod)
plot(rangeVHigh, title="rangeVip", color=color.green)
plot(rangeVLow, title="rangeVip", color=color.yellow)
//Stop loss init variables
var maxProfit = 0.0
var minProfit = 0.0
var stopLossed = 0
//trailing stop loss with fixed entry stop
entryLoss = stopLoss * 0.01
trailingLoss = 30 * 0.01
if (abs(strategy.position_size) >= 1)
    if (strategy.openprofit > strategy.position_avg_price * entryLoss)
        if (strategy.openprofit > maxProfit)
            maxProfit := strategy.openprofit
            minProfit := maxProfit * (1 - trailingLoss)
        else if (strategy.openprofit < minProfit)
            stopLossed := 1
    else if(strategy.openprofit < strategy.position_avg_price * -1 * (entryLoss))
        stopLossed := 1
else
    maxProfit := 0
    minProfit := 0
    stopLossed := 0
//DMA
fastMaPeriod = input(10, minval=1, title="Fast Moving Average")
slowMaPeriod = input(50, minval=1, title="Slow Moving Average")
displacement = input(20, minval=1, title="Displacement")
multiplier = input(10, minval=1, title="Multiplier")
veryFastMa = t3(close, round(multiplier * fastMaPeriod * 8 / 10))
fastMa = t3(close, multiplier * fastMaPeriod)
slowMa = t3(close, multiplier * slowMaPeriod)
laggingLine = offset(fastMa - slowMa, displacement)
currentLine = veryFastMa - slowMa
//trend T3
slowT3 = t3(close, 50)
verySlowT3 = t3(close, 200)
strategy.entry("long", strategy.long, when=((crossover(vipSmooth, rangeVLow) or crossover(vipSmooth, vimSmooth)) and currentLine > laggingLine and slowT3 > verySlowT3 and close > slowT3))
strategy.close("long", when=(stopLossed))
strategy.entry("short", strategy.short, when=((crossunder(vimSmooth, rangeVHigh) or crossover(vimSmooth, vipSmooth)) and currentLine < laggingLine and slowT3 < verySlowT3 and close < slowT3))
strategy.close("short", when=(stopLossed))
Expand (78 lines)