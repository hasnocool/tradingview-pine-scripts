Script Name: MACD controlled risk strategy example
Author: azimuthdynamics
Description: Using a basic MACD as a signal this code is an example of how to base strategies around stops and calculated risk per trade rather than the more common approach of 'equity flipping' long and short for every trade and using an arbitrary %age stop which can leave you a bit exposed, lead to excessive drawdown and miss out on bigger sized positions for more profits.
PineScript code:

Pine Script™ strategy
MACD controlled risk strategy example
Copy code
1
2
3
4
5
6
7
8
9
10
11
12
13
14
15
16
17
18
19
20
21
22
23
24
25
26
27
28
29
30
31
32
33
34
35
36
37
38
39
40
41
42
43
44
45
46
47
48
49
50
//@version=4
strategy( "McDonalds ", shorttitle="Ur Lovin' It", initial_capital=10000, default_qty_type=strategy.cash, currency=currency.USD )
capital_risk    = input( 1.0, "% capital risk per trade" ) / 100
r_exit          = input( 4.0, "Take Profit in 'R'" )
wma_length      = input( 150, 'WMA Bias Length' )
use_eq_ma       = input(title="Use EQ Curve average crossunder to close", type=input.bool, defval=true)
eq_length       = input( 55, 'Equity Curve average length' )
[macd_line, signal_line, hist ] = macd(close, 12, 26, 9)
w_line = wma( close, wma_length )
golong = barssince(crossover(macd_line, signal_line)) <= 5 and ( macd_line < 0 and signal_line < 0 ) and ( close > w_line ) and strategy.opentrades == 0
float stop = na
float tp = na
// For a stop, use a recent low 
stop := golong ? lowest(low, 3)[1] : stop[1]
range = abs(close - stop)
tp := golong ? close + (r_exit * range) : tp[1]
// This is the bit that calculates how much size to use so we only lose 1% of the `strategy.equity`
how_much_willing_to_lose = strategy.equity * capital_risk
// Spread the risk across the stop range 
position_size_in_usd = how_much_willing_to_lose / (range / close)
// Sized specified in base contract
position_size_in_contracts = position_size_in_usd / close
// Enter the position
if golong
    strategy.entry("long", strategy.long, qty=position_size_in_contracts)
    strategy.exit("long exit","long", stop=stop, limit=tp)
eq = ema( strategy.equity, eq_length )
plot( strategy.equity,  color=strategy.equity > 10000 ? color.green : color.red, linewidth=2 )
plot( eq,  color=color.white, linewidth=1, style=plot.style_cross )
if crossunder( strategy.equity, eq ) and use_eq_ma
    strategy.close("long")
    
// experimental exit strategy
// hist_strength = hist >= 0 ? ( hist[1] < hist ? 'strong' : 'weak') : ( hist[1] < hist ? 'weak' : 'strong' )
// if hist < 0 and hist_strength == 'strong' and falling( hist, 8 )
//     strategy.close("long")
Expand (50 lines)