Script Name: T3 Ichimoku Cloud Strategy
Author: Traaaderz
Description: This strategy utilizes a variation of the Ichimoku Cloud based on T3 moving averages to smooth the outline of the cloud. With less noise and conflicting signals, it is an effective strategy when paired with a trailing stop loss.
PineScript code:

Pine Script™ strategy
T3 Ichimoku Cloud Strategy
Copy code
1
2
3
4
5
6
7
8
9
10
11
12
13
14
15
16
17
18
19
20
21
22
23
24
25
26
27
28
29
30
31
32
33
34
35
36
37
38
39
40
41
42
43
44
45
46
47
48
49
50
51
52
53
54
55
56
57
58
59
60
61
62
63
64
65
66
67
68
69
70
71
72
73
74
75
76
77
78
79
80
81
82
83
84
85
86
87
//@version=4
strategy(shorttitle="T3 IC Strategy", title="T3 Ichimoku Cloud Strategy", overlay=true, initial_capital=500)
//t3 constants
b = 0.7
c1 = -b*b*b
c2 = 3*b*b+3*b*b*b
c3 = -6*b*b-3*b-3*b*b*b
c4 = 1+3*b+b*b*b+3*b*b
t3(len) => c1 * ema(ema(ema(ema(ema(ema(close, len), len), len), len), len), len) + c2 * ema(ema(ema(ema(ema(close, len), len), len), len), len) + c3 * ema(ema(ema(ema(close, len), len), len), len) + c4 * ema(ema(ema(close, len), len), len)
//t3 constants end
//Ichimoku inputs
conversionPeriods = input(9, minval=1, title="Conversion Line Periods"),
basePeriods = input(26, minval=1, title="Base Line Periods")
laggingSpan2Periods = input(52, minval=1, title="Lagging Span 2 Periods"),
pivotPeriods = input(52, minval=1, title="Pivot Periods"),
displacement = input(26, minval=1, title="Displacement")
//Ichimoku calcs
conversionLine = t3(conversionPeriods)
baseLine = t3(basePeriods)
leadLine1 = offset(avg(conversionLine, baseLine),displacement -1)
leadLine2 = offset(t3(laggingSpan2Periods),displacement -1)
//pivot calcs
donchian(len) => avg(lowest(len), highest(len))
conversionLineAlt = donchian(conversionPeriods)
baseLineAlt = donchian(basePeriods)
leadLine1Alt = avg(conversionLineAlt, baseLineAlt)
leadLine2Alt = donchian(laggingSpan2Periods)
pivot = avg(lowest(avg(leadLine1Alt, leadLine2Alt),pivotPeriods), highest(avg(leadLine1Alt, leadLine2Alt),pivotPeriods))
//pivot end
//Ichimoku plot
p1 = plot(leadLine1, color=color.green, title="Lead 1")
p2 = plot(leadLine2, color=color.red, title="Lead 2")
p3 = plot(pivot, color=color.purple, title="Pivot")
fill(p1, p2, color = leadLine1 > leadLine2 ? color.green : color.red)
//t3 MA plot
trendT3 = t3(75)
plot(trendT3, "trendT3", color=color.blue)
//other calcs
priceFastSma = sma(close,2) //8
plot(priceFastSma, "priceSma", color=color.purple)
aboveCloud = priceFastSma > leadLine1 and priceFastSma > leadLine2
underCloud =  priceFastSma < leadLine1 and priceFastSma < leadLine2
//Stop loss init variables
var maxProfit = 0.0
var minProfit = 0.0
var stopLossed = 0
//trailing stop loss with fixed entry stop
entryLoss = 0.3 *0.01 // in percent
trailingLoss = 40 * 0.01 // in percent
if (abs(strategy.position_size) >= 1)
    if (strategy.openprofit > strategy.position_avg_price * entryLoss)
        if (strategy.openprofit > maxProfit)
            maxProfit := strategy.openprofit
            minProfit := maxProfit * (1 - trailingLoss)
        else if (strategy.openprofit < minProfit)
            stopLossed := 1
    else if(strategy.openprofit < strategy.position_avg_price * -1 * (entryLoss))
        stopLossed := 1
else
    maxProfit := 0
    minProfit := 0
    stopLossed := 0
strategy.entry("long", strategy.long, 1, when=(((crossover(priceFastSma,leadLine1) or crossover(priceFastSma,leadLine2)) and aboveCloud  and close > pivot and close > trendT3)))
strategy.close("long", when=(stopLossed)) // stop loss
strategy.entry("short", strategy.short, 1, when=(((crossunder(priceFastSma,leadLine1) or crossunder(priceFastSma,leadLine2)) and underCloud and close < pivot and close < trendT3)))
strategy.close("short", when=(stopLossed)) // stop loss
Expand (87 lines)